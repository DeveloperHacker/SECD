// Арифметические операции

// Список из N констант
NIL
LDC [ 1 2 3 4 5 0 ] // -- id операций
CONS
// ..

DUM LDF // Эта функция нужна чтобы вшить список констант в функции объявленные ниже
[
//  Список из M функций
    LDF
    [
        LD [ 1 2 ] LD [ 2 6 ] EQ SEL
        [
            LD [ 1 3 ]
        ]
        [
            LD [ 1 4 ] LD [ 1 1 ] CONS LD [ 1 1 ] AP
            LD [ 1 3 ] LD [ 1 1 ] CONS LD [ 1 1 ] AP

            LD [ 1 2 ] LD [ 2 1 ] EQ SEL [ ADD ]
            [
                LD [ 1 2 ] LD [ 2 2 ] EQ SEL [ MUL ]
                [
                    LD [ 1 2 ] LD [ 2 3 ] EQ SEL [ SUB ]
                    [
                        LD [ 1 2 ] LD [ 2 4 ] EQ SEL [ DIV ]
                        [
                            LD [ 1 2 ] LD [ 2 5 ] EQ SEL [ REM ] [ ]
                        ]
                    ]
                ]
            ]
        ]
        RTN
    ]
//  ..

//  При вызове функции первые M мест в списке аргументов зарезервированы
    LDF
    [
        // id = 0 оставить список без изменей
        // id = 1 сложить два числа
        // id = 2 умножить два числа
        // id = 3 из первого вычесть второе число
        // id = 4 целая часть от деления первого на второе число
        // id = 5 остаток от деления первого на второе число
        LDC [ 2 [ 1 [ 2 [ 1 [ 0 5 ] [ 0 2 ] ] [ 0 10 ] ] [ 2 [ 1 [ 0 3 ] [ 0 4 ] ] [ 0 5 ] ] ] [ 0 8 ] ]
        // список аргументов для орифметических операций, где голова подсписка соответсвует id операции.

        LD 1 CONS
        LD 1 AP RTN
    ]
    AP RTN
]
AP STOP
